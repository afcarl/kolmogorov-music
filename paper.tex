% Based on the template for sigplanconf LaTeX Class by Paul C. Anagnostopoulos (paul@windfall.com)
\documentclass[numbers]{sigplanconf}

\usepackage{amsmath}
\usepackage{hyperref}

\newcommand{\cL}{{\cal L}}

\begin{document}

\special{papersize=8.5in,11in}
\setlength{\pdfpageheight}{\paperheight}
\setlength{\pdfpagewidth}{\paperwidth}

\conferenceinfo{FARM 2016}{September 24, 2016, Nara, Japan}
\copyrightyear{2016}
%\copyrightdata{978-1-nnnn-nnnn-n/yy/mm}
%\copyrightdoi{nnnnnnn.nnnnnnn}

\title{Algomusicology, ???, Profit}
%\subtitle{Subtitle Text, if any}

\authorinfo{Chris Ford}
           {ThoughtWorks}
           {christophertford@gmail.com}

\maketitle

\begin{abstract}
In this paper I propose a novel method for converting algomusicology into profit. While my particular example
is tongue-in-cheek, it shows that algorithmic composition and analysis can help us deconstruct and disrupt
traditional notions of musical authorship.
\end{abstract}

%\category{CR-number}{subcategory}{third-level}

%\keywords
%keyword1, keyword2

\section{Introduction}

The South Park Underpants Gnomes are renowned for their business plan:
\begin{enumerate}
    \item Collect underpants
    \item ?
    \item Profit
\end{enumerate}

A common interpretation of the Gnomes' business is as a satire on Silicon Valley startups. Art critics, literary critics,
algomusicologists and start-uppers have a challenge in common with the Underpants Gnomes
- we may have collected a considerable quantity of underpants/art/users, but how are we to turn that into profit?

Less glibly, is algomusicology doomed to be a mere observer and explainer of musical practice, or might it become an
active participant? Computer science is able to influence industry, though that might take longer than we'd like.
\footnote{Alonzo Church developed the lambda calculus in the 1930s. Java gained lambda expressions in 2015.} Can
algomusicology influence music?\footnote{This paper appears to be the first reference to algomusicology. I believe the term,
which is a portmanteau of algorave and musicology, has a self-evident definition.}

This paper will argue that it can. It will do so by citing examples of existing art works that are algorithmic in nature, and
also by using algomusicological concepts to create a work that challenges our present conception of musical authorship and
copyright.

The examples in this paper are all written in Clojure, a dialect of Lisp. I also rely on Leipzig, a music theory library,
and Midje, a unit testing library.\footnote{Midje allows us to write a unit test as (fact expression => result),
which asserts that expression evaluates to result.}

\section{Air on the {\textbackslash}G String}
Very great quantities of data can exhibit very little complexity. Consider the following Clojure expression, which
evaluates to a sequence of $10^{15}$ uppercase G characters:

\begin{verbatim}
(repeat (* 1000 1000 1000 1000 1000) \G)
\end{verbatim}

Though storing such a string would require roughly a petabyte of memory, representing it indirectly as a computation
takes very little space at all. To put it in perspective, that's roughly the size all music ever recorded,
described in less than a half a tweet\footnote{Based on the iTunes library featuring approximately 50 million songs
of approximately 4 megabytes each, and assuming it represents approximately a fifth of all recorded music. Tweets are
restricted to 140 characters.}.

We can use Clojure's macro facility to build a crude framework for comparing the size of descriptions and their
resulting values. The following two definitions are identical, save that \verb|description-length| is a macro and therefore
operates on the unevaluated expression that is passed to it.

\begin{verbatim}
(defn result-length [expression]
  (-> expression print-str count))

(defmacro description-length [expression]
  (-> expression print-str count))
\end{verbatim}

Here is an example of using these definitions to calculate the description and result length of a short sequence of Gs.

\begin{verbatim}
(fact
  (result-length (repeat 65 \G)) => 131
  (description-length (repeat 65 \G)) => 13)
\end{verbatim}

The explanatory power of a description can be measured by comparing the length of the textual description and the length
of the resulting value. Explanatory power is equivalent to the compression power, where representing a value by an
expression is viewed as a compression strategy.

If there is no way to describe a value more concisely than the value itself, we say that it is random. In other words an
explanatory power of one or less is no explanation at all. In compression terms, there is no possible space saving
to be gained by representing this value by an expression.

\begin{verbatim}
(defmacro explanatory-power [expression]
  `(/ (result-length ~expression)
      (description-length ~expression)))
\end{verbatim}

The explanatory power of our expression for producing 65 Gs is high. A repetitive string of identical characters is
highly redundant, highly compressible and hence able to be described concisely. The explanatory power of this way of
representing the string of Gs this way is over 10.

\begin{verbatim}
(fact
  (explanatory-power (repeat 65 \G)) => 131/13)
\end{verbatim}

There are many expressions that evaluate to the same value, so there are likely to be different expressions of varying
explanatory power. The Kolmogorov complexity is defined as the length of the briefest possible description. This is not,
in general, computable, so it's not possible to determine whether we have obtained the best possible explanation for a
piece of data.

\section{Analysis by compression}

David Meredith has used Kolmogorov complexity to compare the quality of musical analyses. The theory goes that the
more concise the algorithmic description of a piece, the better the musical model.

Using this idea, we can show that composing "Row, row, row your boat" out of sequences of durations and pitches
is a better description of the piece than the raw notes.

\begin{verbatim}
(def row-row
  "A simple melody built from durations and pitches."
              ; Row, row, row  your boat,
  (->> (phrase [3/3  3/3  2/3  1/3  3/3]
               [  0    0    0    1    2])
       (then
                ; Gent-ly   down the  stream,
         (phrase [2/3  1/3  2/3  1/3  6/3]
                 [  2    1    2    3    4]))
       (then    ; Merrily, merrily, merrily, merrily,
         (phrase (repeat 12 1/3)
                 (mapcat (partial repeat 3) [7 4 2 0])))
       (then
                ; Life is   but  a    dream!
         (phrase [2/3  1/3  2/3  1/3  6/3]
                 [  4    3    2    1    0]))
       (canon/canon (canon/simple 4))
       (where :pitch (comp scale/A scale/major))))
\end{verbatim}

Using a little more macro magic, we can calculate the ratio between the above description and the literal notes
of the song. The \verb|definitional| macro allows us to use the symbol \verb|row-row| in our fact notation, rather than
having to paste in the entire expression.

\begin{verbatim}
(defmacro definitional [macro sym]
  (let [value (-> sym repl/source-fn read-string last)]
    `(~macro ~value)))

(fact
  (definitional description-length row-row) => 281
  (definitional result-length row-row) => 2037
  (definitional explanatory-power row-row) => 2037/281)
\end{verbatim}

\section{The Library of Babel}

The Kleene star is the set of all possible substrings built from a set of elements.

\begin{verbatim}
(defn kleene* [elements]
  (letfn [(expand [strings]
            (for [s strings e elements] (conj s e)))]
    (->>
      (lazy-seq (kleene* elements))
      expand
      (cons []))))
\end{verbatim}

We can use the Kleene star with an alphabet of the ASCII alphabet to construct all possible ASCII strings as a lazy
sequence. This results in something very similar to the Library of Babel as imagined by Jorge Luis Borges. In this
vast building of library, are all possible books of four hundred and ten pages in length.

The difference is that our sequence has all possible strings of all finite lengths.

\begin{verbatim}
(defn library-of-babel []
  (let [ascii (->> (range 32 127) (map char))]
    (->> ascii
       kleene*
       (map (partial apply str)))))

(fact
  (->> (library-of-babel) (take 3)) => ["" " " "!"]
  (nth (library-of-babel) 364645) => "GEB")
\end{verbatim}

If we take the Kleene star of a particular alphabet, order the results countably, and then concatenate them, we get
a disjunctive sequence or lexicon. A lexicon has the property that every finite string is a substring.

The result of the Kleene star are interesting from a Kolmogorov complexity perspective, because they can be described
concisely yet contain multitudes. Paradoxically, it displays the property that the whole is simpler than (almost all of)
the parts. That is, the Kolmogorov complexity of one string in our Library of Babel is more complicated to describe
than the Library taken as a whole.

Borges appreciated this paradox. His anonymous narrator reports that on "some shelf in some hexagon (men reasoned)
there must exist a book which is the formula and perfect compendium of all the rest"?????. Any of the many books that contain
our \verb|library-of-babel| function match the narrator's description, though the books that also contain the rest of this paper
must surely provide more helpful context.

\section{Contact}

In Carl Sagan's novel 'Contact', he imagines that a message to humanity is encoded in the digits of pi. If we design an
appropriate encoding scheme, we could do a similar thing and treat the digits of pi as music. However, we would encounter
a similar problem to Borges' librarians - we suspect that every sequence of data we could possibly want is in there
somewhere, but we don't know where to look.

If pi does have every possible of digits within its decimal expansion, then it's a disjunctive
sequence or lexicon. But we don't know for sure.

Fortunately, there are other transcendental numbers that are more predictable. The Champernowne word is a constant whose
decimal expansion is the concatenation of the digits of the natural numbers. The Champernowne word is definitively a lexicon,
which can be concluded by observing that it eventually contains any natural number. Any sequence of digits corresponds to
a natural number, so the word must contain all sequences of digits.

Of course, the sequence of digits may occur earlier too - the string "34" first occurs when the natural numbers three and
four are serialised, well before the natural number 34 is.

\begin{verbatim}
(defn decompose-into-digits [n]
  (let [[remainder quotient] ((juxt mod quot) n 10)]
    (if (zero? quotient)
      [remainder]
      (conj (decompose-into-digits quotient) remainder))))

(defn champernowne-word
  [skip-to]
  (->> (range)
       (map (partial + skip-to))
       (mapcat decompose-into-digits)))

(fact
  (->> (champernowne-word 0) (take 16))
    => [0 1 2 3 4 5 6 7 8 9 1 0 1 1 1 2])
\end{verbatim}

Here we generate the Champernowne word in the way that it is normally done. We start at zero, and decompose the digits of
every subsequent natural number. However, we could just as easily start at any other natural number, which is demonstrated
by the \verb|skip-to| argument. This is a useful property, because it lets us jump straight to any part of the constant
without having to generate all the previous digits.

\section{Anti}

A notorious example. The British government of the time wished to discourage rave culture, so it passed legislation that banned
music with "repetitive beats"?????. Autechre responded with a track in which every bar features a unique beat?????.

\section{Blurred Lines}

'Blurred Lines' is a pop song that was written by Pharrell Williams and Robin Thicke. In March 2015,
they were found to have infringed on the copyright of the Marvin Gaye song 'Got to Give It Up'.

As a response to this situation, I decided to compose a track that would infringe on the copyright of both songs. The following
piece decodes the Champernowne word into music.

\begin{verbatim}
(defn copyright-infringement-song
  [skip-to]
  (->>
    (champernowne-word skip-to)
    (coding/decode 3)
    (tempo (bpm 120))))
\end{verbatim}

As the Champernowne word's digits are predictable, I am able to skip ahead to any part of the song I choose. Here
is the value of \verb|skip-to| that results in a passable version of 'Blurred Lines'.\footnote{The encoding scheme is chosen
to be reversible, which allowed me to determine where in the Champernowne word 'Blurred Lines' would occur. Full details are in the accompanying code.}

\begin{verbatim}
(def gaye-williams-thicke-constant
  12450012001200311273127612731276127312761273127612731...
  27612731276124500120012124500120012003112731276127312...
  76127312761273127612731276127312761245001200121245001...
  20012003112731276127312761273127612731276127312761273...
  12761245001200121245001200120031127312761273127612731...
  27612731276127312761273127612450012001212400012001200...
  31126812711268127112681271126812711268127112681271124...
  00012001212400012001200311268127112681271126812711268...
  12711268127112681271124000120012124000120012003112681...
  27112681271126812711268127112681271126812711240001200...
  121252004100411264125012621249126112471245)
\end{verbatim}

There is nothing in the description of the \verb|copyright-infringement-song| that is in any way related to 'Blurred Lines'.
It is simply a straightforward musical rendering of a well-known mathematical constant derived from the decimal counting
system.

What's more, every other song is also somewhere in there.

Here we finally come to the profit generation strategy - copyright litigation. As the \verb|copyright-infringement-song| is
a universal piece of music, any work published subsequent to it infringes on its copyright.\footnote{I am not a lawyer. Do not rely on my legal advice.}

\section{Conclusion}

\begin{enumerate}
    \item Algomusicology
    \item Copyright litigation
    \item Profit!
\end{enumerate}

\bibliographystyle{plainnat}

% The bibliography should be embedded for final submission.

\begin{thebibliography}{}
\softraggedright

\bibitem{Overtone} Aaron, Sam and Rose, Jeff: Overtone (\url{http://overtone.github.io})
\bibitem{Anti EP} Autechre: Anti EP (\url{https://en.wikipedia.org/wiki/Anti_EP})

\end{thebibliography}

\end{document}
